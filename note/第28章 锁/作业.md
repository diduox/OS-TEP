#### 作业

程序 x86.py 允许你看到不同的线程交替如何导致或避免竞争条件。请参阅 README 文件，了解程序如何工作及其基本输入的详细信息，然后回答以下问题。

#### 问题 

##### 1．首先用标志-p flag.s 运行 x86.py。该代码通过一个内存标志“实现”锁。你能理解 汇编代码试图做什么吗？

```assembly
.var flag
.var count

.main
.top

.acquire
mov  flag, %ax      # get flag
test $0, %ax        # if we get 0 back: lock is free!
jne  .acquire       # if not, try again
mov  $1, flag       # store 1 into flag

# critical section
mov  count, %ax     # get the value at the address
add  $1, %ax        # increment it
mov  %ax, count     # store it back

# release lock
mov  $0, flag       # clear the flag now

# see if we're still looping
sub  $1, %bx
test $0, %bx
jgt .top	

halt
```

试图给count 加上 bx + 1 的值，通过循环。

##### 2．使用默认值运行时，flag.s 是否按预期工作？ 它会产生正确的结果吗？使用-M 和-R 标志跟踪变量和寄存器（并打开-c 查看它们的 值）。你能预测代码运行时标志最终会变成什么值吗？

正常工作，正确结果。count = 2,flag = 0。

##### 3．使用-a 标志更改寄存器%bx 的值（例如，如果只运行两个线程，就用-a bx = 2，bx =  2）。代码是做什么的？对这段代码问上面的问题，答案如何？

count = 4,flag = 0。没有问题（因为默认值的中断频率是50，所以不会在单个线程内发生中断）。

##### 4．对每个线程将 bx 设置为高值，然后使用-i 标志生成不同的中断频率。什么值导致产 生不好的结果？什么值导致产生良好的结果？

i == 1,2,3,4,6,7,8,9,10,12,3......的时候产生不好的结果

i == 11,22,33,44.....的时候产生好的后果（就是跑完一次完整的程序再中断。）

##### 5．现在让我们看看程序 test-and-set.s。首先，尝试理解使用 xchg 指令构建简单锁原语 的代码。获取锁怎么写？释放锁如何写？

```assembly
.main
.top	

.acquire
mov  $1, %ax        
xchg %ax, mutex     # atomic swap of 1 and mutex
test $0, %ax        # if we get 0 back: lock is free!
jne  .acquire       # if not, try again

# critical section
mov  count, %ax     # get the value at the address
add  $1, %ax        # increment it
mov  %ax, count     # store it back

# release lock
mov  $0, mutex

# see if we're still looping
sub  $1, %bx
test $0, %bx
jgt .top	

halt
```

获取锁就是交换ax 与 mutex 的值，释放锁就是把mutex设为0。

**6．现在运行代码，再次更改中断间隔（-i）的值，并确保循环多次。代码是否总能按 预期工作？有时会导致 CPU 使用率不高吗？如何量化呢？**

代码总能按预期进行，看一看用于自旋的代码占总体运行代码的百分比。CPU使用率不高。用休眠代替自旋。

##### 7．使用-P 标志生成锁相关代码的特定测试。例如，执行一个测试计划，在第一个线程 中获取锁，但随后尝试在第二个线程中获取锁。正确的事情发生了吗？你还应该测试什么？

> -P lets you specify exactly which threads run when;
> e.g., 11000 would run thread 1 for 2 instructions, then thread 0 for 3, then repeat

正确的事情发生了，第二个线程没有获取到锁，并且一直自旋。

测试一下锁第一个线程的锁释放之后，第二个线程能否获取这个锁。

##### 8．现在让我们看看 peterson.s 中的代码，它实现了 Person 算法（在文中的补充栏中提到）。 研究这些代码，看看你能否理解它。

```assembly
# array of 2 integers (each size 4 bytes)
# load address of flag into fx register
# access flag[] with 0(%fx,%index,4)
# where %index is a register holding 0 or 1
# index reg contains 0 -> flag[0], if 1->flag[1]
.var flag   2     

# global turn variable
.var turn

# global count
.var count

.main

# put address of flag into fx
lea flag, %fx  	;Load Effective Address 将地址加载到寄存器中

# assume thread ID is in bx (0 or 1, scale by 4 to get proper flag address)
mov %bx, %cx   # bx: self, now copies to cx
neg %cx        # cx: - self
add $1, %cx    # cx: 1 - self

.acquire
mov $1, 0(%fx,%bx,4)    # flag[self] = 1 ; fx + bx * 4
mov %cx, turn           # turn       = 1 - self

.spin1
mov 0(%fx,%cx,4), %ax   # flag[1-self]
test $1, %ax            
jne .fini               # if flag[1-self] != 1, skip past loop to .fini

.spin2                  # just labeled for fun, not needed
mov turn, %ax
test %cx, %ax           # compare 'turn' and '1 - self'
je .spin1               # if turn==1-self, go back and start spin again

# fall out of spin
.fini

# do critical section now
mov count, %ax
add $1, %ax
mov %ax, count

.release
mov $0, 0(%fx,%bx,4)    # flag[self] = 0


# end case: make sure it's other's turn
mov %cx, turn           # turn       = 1 - self
halt
```

就是看对面有没有占着锁，只有在自己有锁且对面没有锁的情况下，才能进入临界区。

即使对面占着锁，我们也可以先占着，但是要等待对面把锁还回去之后，我们才能进入临界区。

在汇编的写法中要加入`-a bx=0,bx=1`,就是要指定线程的编号。

##### 9．现在用不同的-i 值运行代码。你看到了什么样的不同行为？

呵呵，什么不同行为都没看到。就是中断长了会自旋。

##### 10．你能控制调度（带-P 标志）来“证明”代码有效吗？你应该展示哪些不同情况？ 考虑互斥和避免死锁

能，考虑是否互斥。是否锁还回去了还能用。是否锁在用着的时候拿不了，或者能不能同时拿锁。

##### 11．现在研究 ticket.s 中 ticket 锁的代码。它是否与本章中的代码相符？

```assembly
.var ticket
.var turn
.var count

.main
.top	

.acquire
mov $1, %ax
fetchadd %ax, ticket  # grab a ticket ;获取并增加 卧槽add居然不是线程安全的
									  ;因为不是在一个时钟周期里面？
.tryagain							  ;真是震撼我心了
mov turn, %cx         # check if it's your turn 
test %cx, %ax
jne .tryagain

# critical section
mov  count, %ax       # get the value at the address
add  $1, %ax          # increment it
mov  %ax, count       # store it back

# release lock
mov $1, %ax
fetchadd %ax, turn

# see if we're still looping
sub  $1, %bx
test $0, %bx
jgt .top	


halt
```

相符。

##### 12．现在运行代码，使用以下标志：-a bx=1000, bx=1000（此标志设置每个线程循环 1000 次）。看看随着时间的推移发生了什么，线程是否花了很多时间自旋等待锁？

发生了很长时间等待自旋锁。（超过一般半多的时间都用于等待锁）。

##### 13．添加更多的线程，代码表现如何？

还是有80%的时间在自旋，代码表现好像没变。（理论上应该是 1 / N - 1 的有用执行情况才对）

##### 14．现在来看 yield.s，其中我们假设 yield 指令能够使一个线程将 CPU 的控制权交给另 一个线程（实际上，这会是一个 OS 原语，但为了简化仿真，我们假设 有一个指令可以完 成任务）。找到一个场景，其中 test-and-set.s 浪费周期旋转，但 yield.s 不会。节省了多少指令？这些节省在什么情况下会出现？

```assembly
.var mutex
.var count

.main
.top	

.acquire
mov  $1, %ax        
xchg %ax, mutex     # atomic swap of 1 and mutex
test $0, %ax        # if we get 0 back: lock is free!
je .acquire_done    
yield               # if not, yield and try again
j .acquire
.acquire_done

# critical section
mov  count, %ax     # get the value at the address
add  $1, %ax        # increment it
mov  %ax, count     # store it back

# release lock
mov  $0, mutex

# see if we're still looping
sub  $1, %bx
test $0, %bx
jgt .top	

halt

```

在bx较大且时钟周期较长的情况下，test-and-set.s会浪费一个较大的时钟周期，而yield不会。或者说在两个线程的情况下，yield根本不会自旋。

##### 15．最后来看 test-and-test-and-set.s。这把锁有什么作用？与 test-and-set.s 相比，它实现 了什么样的优点？

```assembly
.var mutex
.var count

.main
.top	

.acquire
mov  mutex, %ax
test $0, %ax
jne .acquire
mov  $1, %ax        
xchg %ax, mutex     # atomic swap of 1 and mutex
test $0, %ax        # if we get 0 back: lock is free!
jne .acquire        # if not, try again

# critical section
mov  count, %ax     # get the value at the address
add  $1, %ax        # increment it
mov  %ax, count     # store it back

# release lock
mov  $0, mutex

# see if we're still looping
sub  $1, %bx
test $0, %bx
jgt .top	

halt
```

减少了回写操作，只有在lock空闲的时候，才尝试去获取锁。

[Test and test-and-set - 维基百科，自由的百科全书 (wikipedia.org)](https://en.wikipedia.org/wiki/Test_and_test-and-set)